plugins {
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'dev.huskuraft.fuse'

    fuseJar {

        mergeServiceFiles()

        if (findProject(":fabric:mc-${minecraft_version.replace(".", "-")}") != null) {
            includeJar {
                source project(":fabric:mc-${minecraft_version.replace(".", "-")}")
                task 'remapJar'
            }
        }

        if (findProject(":forge:mc-${minecraft_version.replace(".", "-")}") != null) {
            includeJar {
                source project(":forge:mc-${minecraft_version.replace(".", "-")}")
                task 'shadowJar'
            }
        }

    }

    apply from: '../../gradle/publishing/modrinth.gradle'

    version = mod_version
    group = "${mod_group_id}"

    base {
        archivesName = "${mod_id}-${minecraft_version}"
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    assemble {
        dependsOn fuseJar
    }

    fuseJar {
        dependsOn jar
        archiveClassifier.set('')
    }

    modrinth {
        uploadFile = fuseJar.archiveFile
    }

    def order = parent.subprojects.toList().indexOf(it);
    if (order != 0) {
        tasks.modrinth.dependsOn(parent.subprojects[order - 1].tasks.modrinth)
    }

}
